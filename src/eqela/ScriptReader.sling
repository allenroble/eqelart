
/*
 * This file is part of Eqela Runtime
 * Copyright (c) 2018 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; version 3 of the License.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

class:

func tryReadFile(file as File, code as Script) static
{
	if not file:
		throw ScriptException.forError(SourceReference.forFile(file), "null file")
	if not code:
		throw ScriptException.forError(SourceReference.forFile(file), "No code object given")
	var it = file.readLines()
	if not it:
		throw ScriptException.forError(SourceReference.forFile(file), "Failed to read file")
	var linen = 0
	var function as ScriptFunction
	loop {
		var line = it.next()
		if not line:
			break
		linen ++
		line = String.strip(line)
		if String.isEmpty(line) || String.startsWith(line, "#"):
			continue
		var src = SourceReference.forLine(file, linen, line)
		while String.endsWith(line, "\\") {
			var xt = it.next()
			if not xt:
				break
			linen ++
			xt = String.strip(xt)
			if String.isEmpty(xt) || String.startsWith(xt, "#"):
				continue
			line = String.strip(String.getSubString(line, 0, String.getLength(line) - 1)) .. " " .. xt
		}
		if function {
			if line == "}" {
				function = null
				continue
			}
			var words = String.split(line, ' ', 2)
			if Vector.getSize(words) < 1:
				throw ScriptException.forError(src, "Invalid syntax for statement")
			var cmd = ScriptStatement.forCommand(Vector.get(words, 0))
			cmd.setSource(src)
			foreach arg in String.quotedStringToVector(Vector.get(words, 1), ' '):
				cmd.addToArguments(arg)
			function.addToStatements(cmd)
		}
		else {
			if String.endsWith(line, "{") {
				var defs = String.strip(String.getSubString(line, 0, String.getLength(line)-1))
				var comps = String.split(defs, ':', 2)
				var fname = String.strip(Vector.get(comps, 0))
				if String.isEmpty(fname):
					throw ScriptException.forError(src, "Invalid function name: `" .. fname .. "'")
				var params = new vector<string>
				foreach pp in String.split(Vector.get(comps, 1), ' ') {
					var s = String.strip(pp)
					if String.isNotEmpty(s):
						params += s
				}
				function = new ScriptFunction()
				function.setName(fname)
				function.setSource(src)
				function.setParameters(params)
				code.addToFunctions(function)
				continue
			}
			if String.startsWith(line, "include ") {
				tryReadFile(file.getSibling(String.getSubString(line, 8)), code)
				continue
			}
			if String.startsWith(line, "use ") || String.startsWith(line, "tool ") || String.startsWith(line, "lib ") {
				var cds = Vector.get(String.split(line, ' ', 2), 1)
				var cd = PackageDescription.forString(cds)
				if not cd:
					throw ScriptException.forError(src, "Invalid package description: `" .. cds .. "'")
				cd.setSource(src)
				code.addToUses(cd)
				continue
			}
			if String.startsWith(line, "set ") {
				var words = String.quotedStringToVector(line, ' ')
				if Vector.getSize(words) != 3:
					throw ScriptException.forError(src, "Invalid set statement")
				var key = Vector.get(words, 1)
				if String.isEmpty(key):
					throw ScriptException.forError(src, "Empty key")
				code.setProperty(key, Vector.get(words, 2))
				continue
			}
		}
	}
}

func readFile(ctx as LoggingContext, file as File) static as Script
{
	assert file && file.isFile()
	var v = new Script()
	v.setSourceFile(file)
	v.setExecutionDirectory(file.getParent())
	try {
		tryReadFile(file, v)
	}
	catch(e as Exception) {
		Log.error(ctx, String.asString(e))
		v = null
	}
	return v
}
